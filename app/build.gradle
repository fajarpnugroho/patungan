buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

def version = [major: 1, minor: 0, patch: 0, build: 1]

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.chefcode.android.patungan"
        minSdkVersion 15
        targetSdkVersion 23

        versionCode version.major * 10000 + version.minor * 1000 + version.patch * 100 + version.build
        versionName "${version.major}.${version.minor}.${version.patch}"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""

        multiDexEnabled true
    }
    lintOptions {
        textReport true
        textOutput 'stdout'
        ignore 'InvalidPackage'
    }

    buildTypes {
        debug {
            applicationIdSuffix '.dev'
            versionNameSuffix '-dev'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }
}

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    mavenLocal()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:support-annotations:23.1.1'

    compile 'com.google.code.gson:gson:2.4'

    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'

    compile 'com.google.dagger:dagger:2.0'
    apt 'com.google.dagger:dagger-compiler:2.0'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    compile 'com.jakewharton.timber:timber:4.1.0'
    compile 'com.jakewharton:butterknife:7.0.1'

    compile 'com.squareup.picasso:picasso:2.5.2'

    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0-RC2'

    compile 'com.android.support:multidex:1.0.1'

    compile 'com.firebase:firebase-client-android:2.5.0'

    /* Firebase UI */
    compile 'com.firebaseui:firebase-ui:0.2.2'

    /* For Google Play Services */
    //noinspection GradleCompatible
    compile 'com.google.android.gms:play-services:8.4.0'

    compile 'de.hdodenhof:circleimageview:2.0.0'

    compile ('com.ibm.mobilefirstplatform.clientsdk.android:push:1.0.0')
}
